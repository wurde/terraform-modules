# Define S3 resources.
# https://aws.amazon.com/s3

# https://www.terraform.io/docs/providers/aws/d/iam_role.html
data "aws_iam_role" "main" {
  name = var.role_name
}

# https://www.terraform.io/docs/providers/aws/r/s3_bucket.html
resource "aws_s3_bucket" "main" {
  # The name of the bucket.
  bucket = var.bucket_name

  # A map of tags to assign to the bucket.
  tags = var.tags

  # The canned ACL to apply. Defaults to "private".
  #   https://docs.aws.amazon.com/AmazonS3/latest/dev/acl-overview.html#canned-acl
  acl = "private"

  # Enable versioning. Once you version-enable a bucket, it can never
  #   return to an unversioned state. You can, however, suspend versioning
  #   on that bucket.
  versioning {
    enabled = true
  }

  # Encrypt an object before saving it and decrypt it before downloading it.
  server_side_encryption_configuration {
    rule {
      apply_server_side_encryption_by_default {
        sse_algorithm = "AES256"
      }
    }
  }

  # Define how Amazon S3 manages objects during their lifetime.
  lifecycle_rule {
    # Object key prefix identifying one or more objects to apply the rule.
    prefix = "/"

    # Enable this lifecycle rule.
    enabled = true

    # Number of days after initiating a multipart upload when the multipart
    # upload must be completed.
    abort_incomplete_multipart_upload_days = 2

    # When noncurrent object versions expire.
    noncurrent_version_expiration {
      days = 90
    }
  }

  # All objects (including locked) are deleted when deleting a bucket.
  force_destroy = true
}

# https://www.terraform.io/docs/providers/aws/r/s3_bucket_policy.html
resource "aws_s3_bucket_policy" "domain_policy" {
  bucket = aws_s3_bucket.main.id

  policy = <<POLICY
{
    "Version": "2012-10-17",
    "Statement": [
        {
            "Effect": "Allow",
            "Principal": {
              "AWS": "${data.aws_iam_role.main.arn}"
            },
            "Action": [ "s3:*" ],
            "Resource": [
                "arn:aws:s3:::${var.bucket_name}/*"
            ]
        }
    ]
}
POLICY
}
